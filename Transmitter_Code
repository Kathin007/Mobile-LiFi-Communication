// ESP32 C3 Morse transmitter - send via Serial
int laserPin = 5;
int unit_ms = 300; // 250 ms per dot - must match receiver UNIT

String encodeMorse(char c) {
  switch (c) {
    case 'A': return ".-"; case 'B': return "-..."; case 'C': return "-.-.";
    case 'D': return "-.."; case 'E': return "."; case 'F': return "..-.";
    case 'G': return "--."; case 'H': return "...."; case 'I': return "..";
    case 'J': return ".---"; case 'K': return "-.-"; case 'L': return ".-..";
    case 'M': return "--"; case 'N': return "-."; case 'O': return "---";
    case 'P': return ".--."; case 'Q': return "--.-"; case 'R': return ".-.";
    case 'S': return "..."; case 'T': return "-"; case 'U': return "..-";
    case 'V': return "...-"; case 'W': return ".--"; case 'X': return "-..-";
    case 'Y': return "-.--"; case 'Z': return "--..";
    case '0': return "-----"; case '1': return ".----"; case '2': return "..---";
    case '3': return "...--"; case '4': return "....-"; case '5': return ".....";
    case '6': return "-...."; case '7': return "--..."; case '8': return "---..";
    case '9': return "----.";
    default: return "";
  }
}

void sendMorse(String msg) {
  msg.toUpperCase();
  for (unsigned int i=0; i<msg.length(); ++i) {
    char c = msg[i];
    if (c == ' ') {
      delay(7 * unit_ms); // word gap
      continue;
    }
    String code = encodeMorse(c);
    for (unsigned int j=0; j<code.length(); ++j) {
      digitalWrite(laserPin, HIGH);
      if (code[j] == '.') delay(unit_ms);
      else delay(3 * unit_ms);
      digitalWrite(laserPin, LOW);
      delay(unit_ms); // intra-element gap
    }
    delay(3 * unit_ms); // gap between letters
  }
}

void setup() {
  pinMode(laserPin, OUTPUT);
  digitalWrite(laserPin, LOW);
  Serial.begin(9600);
  Serial.setTimeout(1000);
  Serial.println("Morse ready. Send message over serial.");
}

void loop() {
  delay(2000);
  if (Serial.available()) {
    String s = Serial.readStringUntil('\n');
    s.trim();
    
    if (s.length() > 0) {
      Serial.print("Sending: "); Serial.println(s);
      sendMorse(s);
      Serial.println("Done");
    }
  }
}
